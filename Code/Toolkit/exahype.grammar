//
// This file is part of the ExaHyPE project.
//
// (C) http://exahype.eu
//
// The project has received funding from the European Unionâ€™s Horizon // 2020 research and innovation programme under grant agreement
// No 671698. For copyrights and licensing, please consult the webpage.
//
Package eu.exahype;

Helpers
    all                  =   [ 0 .. 127 ];
    dot                  =   '.';
    digit                =   [ '0' .. '9' ];
    nondigit             =   [ '_' + [ [ 'a' .. 'z' ] + [ 'A' .. 'Z' ] ] ];
    nonzero_digit        =   [ '1' .. '9' ];
    not_star             =   [ all - '*' ];
    not_star_not_slash   =   [ not_star - '/' ];

    filename_element     =  digit | nondigit | '-' | '/' | dot;

    identifier           = nondigit ( digit | nondigit ) *;
    identifier_list_tail = ',' identifier;
    identifier_list      = '{' identifier? identifier_list_tail* '}';

    cr                   =   13;
    lf                   =   10;
    tab                  =   9;
    line_terminator      = lf | cr | cr lf;
    not_line_terminator  = [[all - cr] - lf];


Tokens
  token_end                       = 'end';

  token_project                   = 'exahype-project';

  token_peano_path                = 'peano-path';
  token_tarch_path                = 'tarch-path';
  token_exahype_path              = 'exahype-path';
  token_multiscalelinkedcell_path = 'multiscalelinkedcell-path';
  token_sharedmemoryoracles_path  = 'sharedmemoryoracles-path';
  token_libxsmm_path              = 'libxsmm-path';
  token_output_directory          = 'output-directory';
  token_architecture              = 'architecture';


  token_computational_domain      = 'computational-domain';
  token_dimension                 = 'dimension';
  token_width                     = 'width';
  token_offset                    = 'offset';
  token_maximum_mesh_size         = 'maximum-mesh-size';
  token_end_time                  = 'end-time';

  token_solver                    = 'solver';
  token_aderdg                    = 'ADER-DG';
  token_unknowns                  = 'unknowns';
  token_parameters                = 'parameters';
  token_order                     = 'order';
  token_kernel                    = 'kernel';
  token_language                  = 'language';

  token_plot                      = 'plot';
  token_variable                  = 'variable';
  token_time                      = 'time';
  token_repeat                    = 'repeat';
  token_output                    = 'output';
  token_select                    = 'select';

  token_shared_memory             = 'shared-memory';
  token_cores                     = 'cores';
  token_properties_file           = 'properties-file';
  token_identifier                = 'identifier';

  token_distributed_memory        = 'distributed-memory';

  token_optimisation              = 'optimisation';
  token_fuse_algorithm_steps      = 'fuse-algorithmic-steps';

  token_profiling                 = 'profiling';
  token_profiler                  = 'profiler';
  token_metrics                   = 'metrics';
  token_likwid_inc                = 'likwid_inc';
  token_likwid_lib                = 'likwid_lib';
  token_ipcm_inc                  = 'ipcm_inc';
  token_ipcm_lib                  = 'ipcm_lib';

  token_on_off                    = 'on' | 'off';

  token_equals                = '=';
  token_comma                 = ',';

  blank                       = ( cr | lf | tab | ' ' ) +;
  identifier                  = identifier;
  identifier_list             = identifier_list;
  typename                    = nondigit ( digit | nondigit | '::' ) *;

  int_number                  = digit+;
  float_number                = (digit+ | digit+ '.' digit+);

  filename                    = '/' ? (filename_element + '/') * filename_element +;

  traditional_comment         = '/*' not_star+ '*'+ (not_star_not_slash not_star* '*'+)* '/';
  documentation_comment       = '/**' '*'* (not_star_not_slash not_star* '*'+)* '/';


Ignored Tokens

  traditional_comment,
  documentation_comment,
  blank;



Productions

  project {->project} =
      [begin_token]:token_project [name]:identifier
        paths
        token_architecture token_equals [architecture]:identifier
        computational_domain
        shared_memory?
        distributed_memory?
        optimisation?
        profiling?
        solver_list?
      token_end [end_token]:token_project
        { -> New project( name, paths.paths, architecture, computational_domain.computational_domain, shared_memory.shared_memory, distributed_memory.distributed_memory, optimisation.optimisation, profiling.profiling, [solver_list.solver] ) }
    ;

  paths {->paths} =
     {without_libxsmm}
        token_peano_path                 [peano_path_equals]:token_equals                       [peano_path]:filename
        token_tarch_path                 [tarch_path_equals]:token_equals                       [tarch_path]:filename
        token_multiscalelinkedcell_path  [multiscalelinkedcell_path_equals]:token_equals        [multiscalelinkedcell_path]:filename
        token_sharedmemoryoracles_path   [sharedmemoryoracles_path_equals]:token_equals         [sharedmemoryoracles_path]:filename
        token_exahype_path               [exahype_path_equals]:token_equals                     [exahype_path]:filename
        token_output_directory           [output_directory_equals]:token_equals                 [output_directory]:filename
     { -> New paths(peano_path, tarch_path, multiscalelinkedcell_path, sharedmemoryoracles_path, exahype_path, Null, output_directory) }
     |
     {with_libxsmm}
        token_peano_path                 [peano_path_equals]:token_equals                       [peano_path]:filename
        token_tarch_path                 [tarch_path_equals]:token_equals                       [tarch_path]:filename
        token_multiscalelinkedcell_path  [multiscalelinkedcell_path_equals]:token_equals        [multiscalelinkedcell_path]:filename
        token_sharedmemoryoracles_path   [sharedmemoryoracles_path_equals]:token_equals         [sharedmemoryoracles_path]:filename
        token_exahype_path               [exahype_path_equals]:token_equals                     [exahype_path]:filename
        token_libxsmm_path               [libxsmm_path_equals]:token_equals                     [libxsmm_path]:filename
        token_output_directory           [output_directory_equals]:token_equals                 [output_directory]:filename
     { -> New paths(peano_path, tarch_path, multiscalelinkedcell_path, sharedmemoryoracles_path, exahype_path, libxsmm_path, output_directory) }
    ;

  distributed_memory {->distributed_memory} =
    [begin_token]:token_distributed_memory
      token_identifier [identifier_equals]:token_equals [identifier]:identifier
    token_end [end_token]:token_distributed_memory
      { -> New distributed_memory( identifier ) };

  shared_memory {->shared_memory} =
    [begin_token]:token_shared_memory
      token_identifier [identifier_equals]:token_equals [identifier]:identifier
      token_cores [cores_equals]:token_equals [cores]:int_number
      token_properties_file [properties_equals]:token_equals [properties_file]:filename
    token_end [end_token]:token_shared_memory
      { -> New shared_memory( identifier,cores,properties_file ) };

  computational_domain {->computational_domain} =
     {two_dimensional}
      [begin_token]:token_computational_domain
       token_dimension          [dimension_equals]:token_equals          [dimension]:int_number
       token_width              [width_equals]:token_equals              [width_x]:float_number [width_comma_xy]:token_comma [width_y]:float_number
       token_offset             [offset_equals]:token_equals             [offset_x]:float_number [offset_comma_xy]:token_comma [offset_y]:float_number
       token_maximum_mesh_size  [maximum_mesh_size_equals]:token_equals  [maximum_mesh_size_x]:float_number [maximum_mesh_size_comma_xy]:token_comma [maximum_mesh_size_y]:float_number
       token_end_time           [end_time_equals]:token_equals           [end_time]:float_number
      token_end [end_token]:token_computational_domain
       { -> New computational_domain( dimension, width_x, width_y, Null, offset_x, offset_y, Null, maximum_mesh_size_x, maximum_mesh_size_y, Null, end_time ) } 
       |
     {three_dimensional}
      [begin_token]:token_computational_domain
       token_dimension          [dimension_equals]:token_equals          [dimension]:int_number
       token_width              [width_equals]:token_equals              [width_x]:float_number [width_comma_xy]:token_comma [width_y]:float_number [width_comma_yz]:token_comma [width_z]:float_number 
       token_offset             [offset_equals]:token_equals             [offset_x]:float_number  [offset_comma_xy]:token_comma [offset_y]:float_number [offset_comma_yz]:token_comma [offset_z]:float_number
       token_maximum_mesh_size  [maximum_mesh_size_equals]:token_equals  [maximum_mesh_size_x]:float_number [maximum_mesh_size_comma_xy]:token_comma [maximum_mesh_size_y]:float_number [maximum_mesh_size_comma_yz]:token_comma [maximum_mesh_size_z]:float_number
       token_end_time           [end_time_equals]:token_equals           [end_time]:float_number
      token_end [end_token]:token_computational_domain
       { -> New computational_domain( dimension, width_x, width_y, width_z, offset_x, offset_y, offset_z, maximum_mesh_size_x, maximum_mesh_size_y, maximum_mesh_size_z, end_time ) };

   optimisation {->optimisation} =
      [begin_token]:token_optimisation
        token_fuse_algorithm_steps [token_fuse_algorithm_steps_equals]:token_equals [fuse_algorithm_steps]:token_on_off
      token_end [end_token]:token_optimisation
        { -> New optimisation(fuse_algorithm_steps) }
      ;

   profiling {->profiling} =
      {with_likwid}
       [begin_token]:token_profiling
         token_profiler [token_profiler_equals]:token_equals [profiler]:identifier
         token_metrics [token_metrics_equals]:token_equals [metrics]:identifier_list
         token_likwid_inc [token_likwid_inc_equals]:token_equals [likwid_inc]:filename
         token_likwid_lib [token_likwid_lib_equals]:token_equals [likwid_lib]:filename
       token_end [end_token]:token_profiling
        { -> New profiling(profiler,metrics,likwid_inc,likwid_lib,Null,Null) }
      |
      {with_ipcm}
       [begin_token]:token_profiling
         token_profiler [token_profiler_equals]:token_equals [profiler]:identifier
         token_metrics [token_metrics_equals]:token_equals [metrics]:identifier_list
         token_ipcm_inc [token_ipcm_inc_equals]:token_equals [ipcm_inc]:filename
         token_ipcm_lib [token_ipcm_lib_equals]:token_equals [ipcm_lib]:filename
       token_end [end_token]:token_profiling
        { -> New profiling(profiler,metrics,Null,Null,ipcm_inc,ipcm_lib)}
      |
      {default}
       [begin_token]:token_profiling
         token_profiler [token_profiler_equals]:token_equals [profiler]:identifier
         token_metrics [token_metrics_equals]:token_equals [metrics]:identifier_list
       token_end [end_token]:token_profiling
        { -> New profiling(profiler,metrics,Null,Null,Null,Null) }
      ;

   plot_solution_list {->plot_solution*} =
     [begin_token]:token_plot [plotter_type]:typename
       token_time      [time_equals]:token_equals     [time]:float_number
       token_repeat    [repeat_equals]:token_equals   [repeat]:float_number
       token_output    [output_equals]:token_equals   [output]:filename
       token_select    [select_equals]:token_equals   [select]:identifier_list
     token_end [end_token]:token_plot
     plot_solution_list?
       { -> [New plot_solution(plotter_type,time,repeat,output,select),plot_solution_list.plot_solution] }
     ;

   solver_list {->solver*} =
     {aderdg}
       [begin_token]:token_solver token_aderdg [name]:identifier
         token_unknowns    [unknowns_equals]:token_equals   [unknowns]:int_number
         token_parameters  [parameters_equals]:token_equals [parameters]:int_number
         token_order       [order_equals]:token_equals      [order]:int_number
         token_kernel      [kernel_equals]:token_equals     [kernel]:typename
         token_language    [language_equals]:token_equals   [language]:identifier
         plot_solution_list?
       token_end [end_token]:token_solver
       solver_list?
       { -> [New solver.aderdg(name,unknowns,parameters,order,kernel,language,[plot_solution_list.plot_solution]),solver_list.solver] }
     ;


Abstract Syntax Tree
   project =
     [name]:identifier
     paths
     [architecture]:identifier
     computational_domain
     shared_memory?
     distributed_memory?
     optimisation?
     profiling?
     solver*
     ;

   paths =
     [peano_path]:filename [tarch_path]:filename [multiscalelinkedcell_path]:filename [sharedmemoryoracles_path]:filename [exahype_path]:filename [libxsmm_path]:filename? [output_directory]:filename;

   shared_memory =
     [identifier]:identifier [cores]:int_number [properties_file]:filename;

   distributed_memory =
     [identifier]:identifier;

   computational_domain =
     [dimension]:int_number [width_x]:float_number [width_y]:float_number [width_z]:float_number? [offset_x]:float_number [offset_y]:float_number [offset_z]:float_number? [maximum_mesh_size_x]:float_number [maximum_mesh_size_y]:float_number [maximum_mesh_size_z]:float_number? [end_time]:float_number;

   optimisation =
     [fuse_algorithm_steps]:token_on_off;

   profiling =
     [profiler]:identifier [metrics]:identifier_list [likwid_inc]:filename? [likwid_lib]:filename? [ipcm_inc]:filename? [ipcm_lib]:filename?;

   solver =
     {aderdg} [name]:identifier [unknowns]:int_number  [parameters]:int_number [order]:int_number [kernel]:typename [language]:identifier plot_solution*
     ;

   plot_solution =
     [plotter_type]:typename [time]:float_number [repeat]:float_number [output]:filename [select]:identifier_list;
