//
// This file is part of the ExaHyPE project.
//
// (C) http://exahype.eu
//
// The project has received funding from the European Unionâ€™s Horizon // 2020 research and innovation programme under grant agreement
// No 671698. For copyrights and licensing, please consult the webpage.
//
Package eu.exahype;

Helpers
    all                 =   [ 0 .. 127 ];
    dot                 =   '.';
    digit               =   [ '0' .. '9' ];
    nondigit            =   [ '_' + [ [ 'a' .. 'z' ] + [ 'A' .. 'Z' ] ] ];
    nonzero_digit       =   [ '1' .. '9' ];
    not_star            =   [ all - '*' ];
    not_star_not_slash  =   [ not_star - '/' ];
    
    filename_element =  digit | nondigit | '-' | '/' | dot;

    cr                  =   13;   
    lf                  =   10;
    tab                 =   9;  
    line_terminator     = lf | cr | cr lf; 
    not_line_terminator = [[all - cr] - lf];
    
    
Tokens 
  token_end                   = 'end';

  token_project                   = 'exahype-project';
  token_peano_path                = 'peano-path';
  token_tarch_path                = 'tarch-path';
  token_exahype_kernel_path       = 'exahype-kernel-path';
  token_multiscalelinkedcell_path = 'multiscalelinkedcell-path';
  
  token_equals                = '=';
    
  blank                       = ( cr | lf | tab | ' ' ) +;
  identifier                  = nondigit ( digit | nondigit ) *;

  filename                    = '/' ? (filename_element + '/') * filename_element +;
  
  traditional_comment         = '/*' not_star+ '*'+ (not_star_not_slash not_star* '*'+)* '/';
  documentation_comment       =    '/**' '*'* (not_star_not_slash not_star* '*'+)* '/';
  end_of_line_comment         = '//' not_line_terminator * line_terminator;


Ignored Tokens
  
  traditional_comment,
  documentation_comment,
  end_of_line_comment,
  blank;



Productions
  
  project {->project} = 
    [begin_token]:token_project [name]:identifier 
      token_peano_path                 [peano_path_equals]:token_equals                       [peano_path]:filename  
      token_tarch_path                 [tarch_path_equals]:token_equals                       [tarch_path]:filename  
      token_multiscalelinkedcell_path  [multiscalelinkedcell_path_equals]:token_equals        [multiscalelinkedcell_path]:filename  
      token_exahype_kernel_path        [exahype_kernel_path_equals]:token_equals              [exahype_kernel_path]:filename
    token_end [end_token]:token_project
      { -> New project( name, peano_path, tarch_path, multiscalelinkedcell_path, exahype_kernel_path ) };

    
    
Abstract Syntax Tree
   project = [name]:identifier [peano_path]:filename [tarch_path]:filename [multiscalelinkedcell_path]:filename [exahype_kernel_path]:filename;